/*
 * Run "gradlew" or "gradlew cgeoHelp" in the parent directory for a help of how to use this build file.
 */
apply plugin: 'com.android.application'
apply plugin: 'android-apt'

//Testing guide : http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Testing
//https://github.com/stephanenicolas/Quality-Tools-for-Android

android {
    compileSdkVersion "Google Inc.:Google APIs:22"
    buildToolsVersion "23.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
    	// license files of libs are not needed in our APK
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        versionName versionNameFromDate()
        versionCode versionCodeFromDate()

        // NOTE: must match the package in the test directory and must be different from the app package
        testApplicationId "cgeo.geocaching.test"

        testInstrumentationRunner "cgeo.junit.CgeoTestRunner"

        //testHandlingProfiling true
        testFunctionalTest true
        
        // by convention, the folder name "main" is used for the APK file name. we want cgeo instead
        archivesBaseName = "cgeo"
    }

    //Conditional signing
    //Uncomment and set the 4 key properties (#key.store, ...) in gradle.properties
    signingConfigs {
        release 
    }


    buildTypes {
        debug {
            //zipAlign = true
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            versionNameSuffix "-" + gitCommitId().substring(0, 7)
        }
        release {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }

    testBuildType "debug" //the default BuildType

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            //java.srcDirs = ['src', 'thirdparty', 'annotation_gen']
            java.srcDirs = ['src', 'thirdparty']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        //new instrumentTest name
        androidTest.setRoot('../tests')
        androidTest{
            manifest.srcFile '../tests/AndroidManifest.xml'
            java.srcDirs = ['../tests/java']
            res.srcDirs = ['../tests/res', 'res']
            resources.srcDirs = ['../tests/res', 'res']
        }
    }

    testOptions {
        resultsDir = "$project.buildDir/build/test-results"
    }

    lintOptions {
    	// generally we accept lint errors when building 
        abortOnError false
        
    	// abort release builds in case of FATAL errors
        checkReleaseBuilds true
    }

}

if (project.hasProperty('key.store') && project.hasProperty('key.store.password') && project.hasProperty('key.alias.password')) {
	android.signingConfigs.release.storeFile = file(project.property('key.store'))
	android.signingConfigs.release.storePassword = property('key.store.password')
	android.signingConfigs.release.keyPassword = property('key.alias.password')
	android.signingConfigs.release.keyAlias = property('key.alias')
} else {
	android.buildTypes.release.signingConfig = null
}

dependencies {
    // Android annotations
    apt( "org.androidannotations:androidannotations:$AndroidAnnotationsVersion"){
        exclude module:'androidannotations-api'
    }
    compile "org.androidannotations:androidannotations-api:$AndroidAnnotationsVersion"

    // Maps.ME integration
    compile project(":mapswithme-api")

    // Showcase view
    // TODO replace by repository dependency. however, there is a conflict in min sdk, which needs to be solved then
    compile project(":showcaseview")

    compile files('libs/httpclientandroidlib-1.1.2.jar')
    compile files('libs/locus-api-4.0.jar')
    //https://mapsforge.googlecode.com/files/mapsforge-map-0.2.4.jar
    compile files('libs/mapsforge-map-0.2.4.jar')
    compile files('libs/mapsforge-map-0.3.0-jar-with-dependencies.jar')

    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.google.android.gms:play-services:7.3.0'

    // Annotations for View injection
    compile "com.jakewharton:butterknife:$ButterKnifeVersion"

    // Apache Commons
    compile "org.apache.commons:commons-collections4:$CommonsCollections4Version"
    compile "org.apache.commons:commons-lang3:$CommonsLang3Version"
    compile "commons-io:commons-io:$CommonsIoVersion"
    compile 'com.google.code.findbugs:annotations:3.0.1u2'

    // Reactive extensions
    compile "io.reactivex:rxjava:$RXJavaVersion"
    compile "io.reactivex:rxandroid:$RXAndroidVersion"

    // Jackson XML processing
    compile "com.fasterxml.jackson.core:jackson-core:$JacksonCoreVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$JacksonDatabindVersion"
    compile "com.fasterxml.jackson.core:jackson-annotations:$JacksonAnnotationsVersion"

    // Zxing integration
    compile "com.google.zxing:android-integration:3.2.1"

    //TEST
    //compile files("compile-libs/androidannotations-$AAVersion.jar")
    //compile files('compile-libs/findbugs-ant.jar')
    //compile files('compile-libs/findbugs-jsr305.jar')  //CheckForNull conflict with com.google.code.findbugs:annotations

    // Null annotations
    compile 'org.eclipse.jdt:org.eclipse.jdt.annotation:1.1.0'

    // Undo toast
    // TODO: This dependency does not work correctly and I don't know how to use the local project properly instead
    compile 'com.github.jenzz.undobar:library:1.3:api8Release@aar'
    // compile files("../undobar/libs/undobar-1.3.jar")
    compile 'com.nineoldandroids:library:2.4.0'

    //Robotium / Robolectric??
    androidTestCompile files('../tests/libs/android-junit-report-1.5.8.jar')
}

/*
 ANDROID ANNOTATIONS
 */

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName 'cgeo.geocaching'

        // If you're using Android NBS flavors you should use the following line instead of hard-coded packageName
        // resourcePackageName android.defaultConfig.packageName

        // You can set optional annotation processing options here, like these commented options:
        // logLevel 'INFO'
        // logFile '/var/log/aa.log'
    }
}

/*
*
*
* END of useful build
* Following task are for samples
*
 */

task logInfo {
    logging.captureStandardOutput LogLevel.INFO
    doFirst {
        println 'A task message which is logged at INFO level'
    }
}

/*
 UNIT TEST
 */
/*
sourceSets {
    unitTest {
        java.srcDir file('../tests/src')
        //java.srcDirs =  ['src', 'thirdparty', 'annotation_gen', '../tests']
        resources.srcDir file('../tests/res')
        //manifest.srcFile '../tests/AndroidManifest.xml'
    }
}
dependencies {
    //unitTestCompile files("$project.buildDir/classes/release")
    unitTestCompile files("$project.buildDir/classes/debug")
    unitTestCompile 'junit:junit:4.8.2'
    unitTestCompile 'com.google.android:android-test:4.1.1.4'

    unitTestCompile "org.androidannotations:androidannotations-api:$AAVersion"
    unitTestCompile "com.jakewharton:butterknife:$ButterKnifeVersion"
    unitTestCompile "org.apache.commons:commons-collections4:$CommonsCollections4Version"
    unitTestCompile "commons-io:commons-io:$CommonsIoVersion"
    unitTestCompile 'org.apache.commons:commons-lang3:$CommonsLang3Version'
    unitTestCompile 'com.google.code.findbugs:annotations:3.0.1u2'
    unitTestCompile 'io.reactivex:rxjava-core:$RXVersion'
    unitTestCompile 'io.reactivex:rxjava-android:$RXVersion'
}

// extend the runtime
configurations {
    unitTestCompile.extendsFrom runtime
    unitTestRuntime.extendsFrom unitTestCompile
}

// add the unitTest task
//task unitTest(type:Test, dependsOn: assemble) {
task unitTest(type:Test, dependsOn: assembleDebug) {
    description = "run unit tests"
    testClassesDir = project.sourceSets.unitTest.output.classesDir
    classpath = project.sourceSets.unitTest.runtimeClasspath
}
// bind to check
check.dependsOn unitTest
//build.dependsOn unitTest
*/

//run gradle compileTest

/*
 PLACEHOLDER
 */

//Not working...copy templates/keys.xml in res/values for the moment
task prep() {
    /*
    requiredProperties "maps.api.key", "maps.api.key.market", "ocde.okapi.consumer.key", "ocde.okapi.consumer.secret", "ocpl.okapi.consumer.key", "ocpl.okapi.consumer.secret"
    doFirst {
        logger.info("prep... ")

        copy {
            from "templates"
            include "*.xml"
            into "res/values"
            filter(org.apache.tools.ant.filters.ReplaceTokens, tokens:  project.ext.filterTokens)
        }
    }
    */
}
task prep2() {
/*
        //http://goo.gl/bqnwjJ
        copy{
            from("${buildDir}/manifests"){
                include "${variant.dirName}/AndroidManifest.xml"
            }
            into("${buildDir}/manifests/$variant.name")

            // define a variable for your key:
            def gmaps_key = "<your-key>"

            filter{
                String line -> line.replaceAll("<meta-data android:name=\"com.google.android.maps.v2.API_KEY\" android:value=\"\"/>",
                                               "<meta-data android:name=\"com.google.android.maps.v2.API_KEY\" android:value=\"" + gmaps_key + "\"/>")
            }

            // set the path to the modified Manifest:
            variant.processResources.manifestFile = file("${buildDir}/manifests/${variant.name}/${variant.dirName}/AndroidManifest.xml")
        }


         */
}

android.applicationVariants.all{ variant ->
    variant.mergeResources.doLast{
        logger.info("rootProject.projectDir="+rootProject.projectDir)
        prep{}
    }
}

//http://blog.futurice.com/android_unit_testing_in_ides_and_ci_environments
task addTest {
    def src = ['tests/java']
    def file = file("app.iml")

    doLast {
        try {
            def parsedXml = (new XmlParser()).parse(file)
            def node = parsedXml.component[1].content[0]
            src.each {
                def path = 'file://$MODULE_DIR$/' + "${it}"
                def set = node.find { it.@url == path }
                if (set == null) {
                    new Node(node, 'sourceFolder', ['url': 'file://$MODULE_DIR$/' + "${it}", 'isTestSource': "true"])
                    def writer = new StringWriter()
                    new XmlNodePrinter(new PrintWriter(writer)).print(parsedXml)
                    file.text = writer.toString()
                }
            }
        } catch (FileNotFoundException e) {
            // nop, iml not found
        }
    }
}
/*
// always do the addtest on prebuild
gradle.projectsEvaluated {
    preBuild.dependsOn(addTest)
}*/


/*
 * version number from the current date
 */
def versionCodeFromDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return Integer.valueOf(formattedDate)
}

/* 
 * version name based on current date
 */ 
def versionNameFromDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy.MM.dd')
    return formattedDate
}

/*
 * get the most recent git commit ID
 */
def gitCommitId() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

